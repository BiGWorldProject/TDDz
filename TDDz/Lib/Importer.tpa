OUTER_TEXT_SPRINT COMP_FOLDER ~~

//=============================================================================
DEFINE_ACTION_FUNCTION ImportComponent STR_VAR aComp = ~~
BEGIN	
	OUTER_TEXT_SPRINT COMP_FOLDER ~_%aComp%~
	
	MKDIR ~%MOD_FOLDER%/TEMP~
	
	MKDIR ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/~
	GET_DIRECTORY_ARRAY component_existing_folders ~%MOD_FOLDER%/%COMP_FOLDER%~ ~.*~
	
	MKDIR ~%MOD_FOLDER%/TEMP/Translations~
	LAM ImportTranslationsBeforeAnySAYandCOMPILE_FromTDD
	LAF ImportResources_FromTDD STR_VAR res_type = ~BAM~ res_dest = ~override~ END
	LAF CopyResources STR_VAR res_type = ~BAM~ res_dest = ~override~ END
	LAM ImportSPL_FromTDD
	LAM ImportCRE_FromTDD
	LAM ImportITM_FromTDD	
	LAM ImportSTO_FromTDD
	LAM ImportARE_FromTDD
	LAM ReplaceTDDMiniMapsByEEMiniMaps	
	LAM ReplaceTDDSearchMapsByEESearchMapsAndOthersBMP	
	//
	LAF CopyToTemp STR_VAR res_type = ~BAF~ res_path = ~%MOD_FOLDER%/%COMP_FOLDER%~ END
	LAF CopyToTemp STR_VAR res_type = ~D~ res_path = ~%MOD_FOLDER%/%COMP_FOLDER%~ END
	LAM HandleScriptedXpPatches
	
	ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/%COMP_FOLDER%/%aComp%.tpa~
		BEGIN INCLUDE ~%MOD_FOLDER%/%COMP_FOLDER%/%aComp%.tpa~ LAM FinalizeImport END
		
	LAM AfterUserConfigCompileBAFandD	
	
	ACTION_IF 1
		BEGIN 
			LAM HandleAudio 
			LAM HandleTileset	
		END	
		ELSE BEGIN PRINT ~!!!!!!!! DEBUG ON: HandleAudio and HandleTileset skipped !!!!!!!!~ END
END

//=============================================================================
DEFINE_ACTION_MACRO ReplaceTDDMiniMapsByEEMiniMaps
BEGIN	
	LAF CopyResources STR_VAR res_type = ~MOS~ res_dest = ~override~ END
END

//=============================================================================
DEFINE_ACTION_MACRO ReplaceTDDSearchMapsByEESearchMapsAndOthersBMP
BEGIN	
	LAF CopyResources STR_VAR res_type = ~BMP~ res_dest = ~override~ END
END

//=============================================================================
DEFINE_ACTION_MACRO AfterUserConfigCompileBAFandD
BEGIN	
	COMPILE ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%~
END

//=============================================================================
DEFINE_ACTION_MACRO ImportARE_FromTDD
BEGIN
	INCLUDE ~%MOD_FOLDER%/Lib/Areas.tpa~
	ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/%COMP_FOLDER%/AreasContract.2da~ BEGIN
		COPY ~%MOD_FOLDER%/%COMP_FOLDER%/AreasContract.2da~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/AreasContract.2da~
			COUNT_2DA_ROWS 1 count
			WHILE (count > 1) BEGIN
				READ_2DA_ENTRY (count -1) 0 7 resname
				READ_2DA_ENTRY (count -1) 1 7 mos
				READ_2DA_ENTRY (count -1) 2 7 wed
				READ_2DA_ENTRY (count -1) 3 7 tiz
				READ_2DA_ENTRY (count -1) 4 7 htmap
				READ_2DA_ENTRY (count -1) 5 7 lmmap
				READ_2DA_ENTRY (count -1) 6 7 srmap
				INNER_ACTION BEGIN 
					COPY ~TDD/ARE/%resname%.ARE~ ~override~
						LPM SetAnimationsInCombatFlag
						
					ACTION_IF NOT ~%mos%~ STRING_EQUAL ~+~ BEGIN ACTION_IF IS_AN_INT mos BEGIN COPY ~TDD/MOS/%resname%.MOS~ ~override~ END ELSE BEGIN COPY ~TDD/MOS/%mos%.MOS~ ~override~ END END
					ACTION_IF NOT ~%wed%~ STRING_EQUAL ~+~ BEGIN ACTION_IF IS_AN_INT wed BEGIN COPY ~TDD/WED/%resname%.WED~ ~override~ END ELSE BEGIN COPY ~TDD/WED/%wed%.WED~ ~override~ END END
					ACTION_IF NOT ~%tiz%~ STRING_EQUAL ~+~ BEGIN ACTION_IF IS_AN_INT tiz BEGIN COPY ~TDD/TIZ/%resname%.TIZ~ ~override~ END ELSE BEGIN COPY ~TDD/TIZ/%tiz%.TIZ~ ~%MOD_FOLDER%/TIZ/%tiz%.TIZ~ END END
					ACTION_IF NOT ~%htmap%~ STRING_EQUAL ~+~ BEGIN ACTION_IF IS_AN_INT htmap BEGIN COPY ~TDD/MOS/%resname%HT.BMP~ ~override~ END ELSE BEGIN COPY ~TDD/MOS/%htmap%.BMP~ ~override~ END END
					ACTION_IF NOT ~%lmmap%~ STRING_EQUAL ~+~ BEGIN ACTION_IF IS_AN_INT lmmap BEGIN COPY ~TDD/MOS/%resname%LM.BMP~ ~override~ END ELSE BEGIN COPY ~TDD/MOS/%lmmap%.BMP~ ~override~ END END
					ACTION_IF NOT ~%srmap%~ STRING_EQUAL ~+~ BEGIN ACTION_IF IS_AN_INT srmap BEGIN COPY ~TDD/MOS/%resname%SR.BMP~ ~override~ END ELSE BEGIN COPY ~TDD/MOS/%srmap%.BMP~ ~override~ END END
				END
				SET count = count -1
			END		
		BUT_ONLY
	END
END


//=============================================================================
DEFINE_ACTION_MACRO HandleScriptedXpPatches
BEGIN
	ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/%COMP_FOLDER%/Xp.2da~ BEGIN
		COPY ~%MOD_FOLDER%/%COMP_FOLDER%/Xp.2da~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/Xp.2da~
			COUNT_2DA_ROWS 4 count
			WHILE (count > 1) BEGIN
				READ_2DA_ENTRY (count -1) 0 4 resref
				READ_2DA_ENTRY (count -1) 1 4 signature
				READ_2DA_ENTRY (count -1) 2 4 tddValue
				READ_2DA_ENTRY (count -1) 3 4 xpValue
				SET count = count -1											
						
				INNER_ACTION BEGIN
					COPY ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/%resref%~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/%resref%~
						SET xp = %tddValue%
						PATCH_IF IS_AN_INT xpValue BEGIN
							SET xp = %xpValue%
							PATCH_IF %xpValue% < 1000 
								BEGIN SET xp = %tddValue% * %xpValue% / 100 END
						END
						REPLACE_TEXTUALLY ~%signature%~ ~%xp%~						
				END
			END		
		BUT_ONLY
	END
END


//=============================================================================
DEFINE_ACTION_MACRO ImportITM_FromTDD
BEGIN
	ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/Importer.exe~ BEGIN	
		AT_NOW ~START /b /wait "" "%MOD_FOLDER%/Importer.exe" "%MOD_FOLDER%/%COMP_FOLDER%/ItemsContract.2da" "TDD/ITM" "%MOD_FOLDER%/TEMP/%COMP_FOLDER%/ItemsByContract.tpa" "ITM"~ EXACT
		INCLUDE ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/ItemsByContract.tpa~ LAM ImportItems
	END

	ELSE BEGIN
		ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/%COMP_FOLDER%/ItemsContract.2da~ BEGIN
			COPY ~%MOD_FOLDER%/%COMP_FOLDER%/ItemsContract.2da~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/ItemsContract.2da~
				COUNT_2DA_ROWS 7 count
				WHILE (count > 1) BEGIN
					READ_2DA_ENTRY (count -1) 0 7 resname
					READ_2DA_ENTRY (count -1) 1 7 name1
					READ_2DA_ENTRY (count -1) 2 7 name2
					READ_2DA_ENTRY (count -1) 3 7 unidentifiedDesc
					READ_2DA_ENTRY (count -1) 4 7 desc
					READ_2DA_ENTRY (count -1) 5 7 iconS
					READ_2DA_ENTRY (count -1) 6 7 undroppable
					SET count = count -1
						
					TO_UPPER resname
					INNER_ACTION BEGIN
						OUTER_SPRINT path ~TDD/ITM/%resname%.ITM~
						ACTION_IF ~%resname%~ STRING_EQUAL ~TYRANT~ 
							BEGIN OUTER_SPRINT path ~%MOD_FOLDER%/%COMP_FOLDER%/_copy/%resname%.ITM~ END
							
						COPY ~%path%~ ~override~
							PATCH_IF IS_AN_INT name1 BEGIN SET strref = RESOLVE_STR_REF ((AT name1)) WRITE_LONG NAME1 %strref% END
							PATCH_IF IS_AN_INT name2 BEGIN SET strref = RESOLVE_STR_REF ((AT name2)) WRITE_LONG NAME2 %strref% END
							PATCH_IF IS_AN_INT unidentifiedDesc BEGIN SET strref = RESOLVE_STR_REF ((AT unidentifiedDesc)) WRITE_LONG UNIDENTIFIED_DESC %strref% END
							PATCH_IF IS_AN_INT desc BEGIN SET strref = RESOLVE_STR_REF ((AT desc)) WRITE_LONG DESC %strref% END
							PATCH_IF NOT ~%iconS%~ STRING_EQUAL ~+~ BEGIN 
								PATCH_IF IS_AN_INT iconS 
									BEGIN SPRINT iconS ~%resname%~ END																	
								WRITE_ASCII 0x3A ~%iconS%~ #8
								INNER_ACTION BEGIN
									ACTION_IF NOT FILE_EXISTS_IN_GAME ~%iconS%.bam~ 
										BEGIN COPY ~TDD/BAM/%iconS%.bam~ ~override~ END 
								END									
							END
							PATCH_IF ~%undroppable%~ STRING_EQUAL ~.~ BEGIN READ_BYTE 0x18 THIS WRITE_BYTE 0x18 (%THIS% BAND 0b11111011) END							
					END
				END		
			BUT_ONLY
		END
	END
END


//=============================================================================
DEFINE_ACTION_MACRO ImportSPL_FromTDD
BEGIN	
	ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/Importer.exe~ BEGIN	
		AT_NOW ~START /b /wait "" "%MOD_FOLDER%/Importer.exe" "%MOD_FOLDER%/%COMP_FOLDER%/SpellsContract.2da" "TDD/SPL" "%MOD_FOLDER%/TEMP/%COMP_FOLDER%/SpellsByContract.tpa" "SPL"~ EXACT
		INCLUDE ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/SpellsByContract.tpa~ LAM ImportSpells
	END
		
	ELSE BEGIN	
		ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/%COMP_FOLDER%/SpellsContract.2da~ BEGIN
			COPY ~%MOD_FOLDER%/%COMP_FOLDER%/SpellsContract.2da~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/SpellsContract.2da~
				COUNT_2DA_ROWS 5 count
				WHILE (count > 1) BEGIN
					READ_2DA_ENTRY (count -1) 0 5 resname
					READ_2DA_ENTRY (count -1) 1 5 name1
					READ_2DA_ENTRY (count -1) 2 5 name2
					READ_2DA_ENTRY (count -1) 3 5 unidentifiedDesc
					READ_2DA_ENTRY (count -1) 4 5 desc
					SET count = count -1
						
					INNER_ACTION BEGIN
						COPY ~TDD/SPL/%resname%.SPL~ ~override~
							PATCH_IF IS_AN_INT name1 BEGIN SET strref = RESOLVE_STR_REF ((AT name1)) WRITE_LONG NAME1 %strref% END
							PATCH_IF IS_AN_INT name2 BEGIN SET strref = RESOLVE_STR_REF ((AT name2)) WRITE_LONG NAME2 %strref% END
							PATCH_IF IS_AN_INT unidentifiedDesc BEGIN SET strref = RESOLVE_STR_REF ((AT unidentifiedDesc)) WRITE_LONG UNIDENTIFIED_DESC %strref% END
							PATCH_IF IS_AN_INT desc BEGIN SET strref = RESOLVE_STR_REF ((AT desc)) WRITE_LONG DESC %strref% END
					END
				END
			BUT_ONLY
		END
	END
END


//=============================================================================
DEFINE_ACTION_MACRO ImportSTO_FromTDD
BEGIN	
	ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/Importer.exe~ BEGIN	
		AT_NOW ~START /b /wait "" "%MOD_FOLDER%/Importer.exe" "%MOD_FOLDER%/%COMP_FOLDER%/StoresContract.2da" "TDD/STO" "%MOD_FOLDER%/TEMP/%COMP_FOLDER%/StoresByContract.tpa" "STO" "%MOD_FOLDER%/%COMP_FOLDER%/StoresContract.2da" "TDD/STO" ~ EXACT
		INCLUDE ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/StoresByContract.tpa~ LAM ImportStores
	END
		
	ELSE BEGIN	
		ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/%COMP_FOLDER%/StoresContract.2da~ BEGIN
			COPY ~%MOD_FOLDER%/%COMP_FOLDER%/StoresContract.2da~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/StoresContract.2da~
				COUNT_2DA_ROWS 3 count
				WHILE (count > 1) BEGIN
					READ_2DA_ENTRY (count -1) 0 3 resname
					READ_2DA_ENTRY (count -1) 1 3 name1
					READ_2DA_ENTRY (count -1) 2 3 name2
					SET count = count -1
						
					INNER_ACTION BEGIN
						COPY ~TDD/STO/%resname%.STO~ ~override~
							PATCH_IF IS_AN_INT name1 BEGIN SET strref = RESOLVE_STR_REF ((AT name1)) WRITE_LONG NAME1 %strref% END
							PATCH_IF IS_AN_INT name2 BEGIN SET strref = RESOLVE_STR_REF ((AT name2)) WRITE_LONG NAME2 %strref% END
					END
				END		
			BUT_ONLY
		END
	END
END

//=============================================================================
DEFINE_ACTION_MACRO ImportCRE_FromTDD
BEGIN	
	ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/Importer.exe~ BEGIN
		AT_NOW ~START /b /wait "" "%MOD_FOLDER%/Importer.exe" "%MOD_FOLDER%/%COMP_FOLDER%/CreaturesContract.2da" "TDD/CRE" "%MOD_FOLDER%/TEMP/%COMP_FOLDER%/CreaturesByContract.tpa" "CRE"~ EXACT
		INCLUDE ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/CreaturesByContract.tpa~ LAM ImportCreatures
	END 
	
	ELSE BEGIN	
		ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/%COMP_FOLDER%/CreaturesContract.2da~ BEGIN
			COPY ~%MOD_FOLDER%/%COMP_FOLDER%/CreaturesContract.2da~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/CreaturesContract.2da~
				COUNT_2DA_ROWS 12 count
				WHILE (count > 1) BEGIN
					READ_2DA_ENTRY (count -1) 0 12 resname
					READ_2DA_ENTRY (count -1) 1 12 name1
					READ_2DA_ENTRY (count -1) 2 12 name2
					READ_2DA_ENTRY (count -1) 3 12 deathvar
					READ_2DA_ENTRY (count -1) 4 12 sO
					READ_2DA_ENTRY (count -1) 5 12 sC
					READ_2DA_ENTRY (count -1) 6 12 sR
					READ_2DA_ENTRY (count -1) 7 12 sG
					READ_2DA_ENTRY (count -1) 8 12 sD
					READ_2DA_ENTRY (count -1) 9 12 dg
					READ_2DA_ENTRY (count -1) 10 12 portraitS
					READ_2DA_ENTRY (count -1) 11 12 xpValue
					SET count = count -1
						
					INNER_ACTION BEGIN
						COPY ~TDD/CRE/%resname%.CRE~ ~override~
							PATCH_IF IS_AN_INT name1 BEGIN SET strref = RESOLVE_STR_REF ((AT name1)) WRITE_LONG NAME1 %strref% END
							PATCH_IF IS_AN_INT name2 BEGIN SET strref = RESOLVE_STR_REF ((AT name2)) WRITE_LONG NAME2 %strref% END
							PATCH_IF ~%deathvar%~ STRING_EQUAL ~+~ BEGIN WRITE_ASCII DEATHVAR ~~ #32 END ELSE BEGIN PATCH_IF IS_AN_INT deathvar BEGIN WRITE_ASCII DEATHVAR ~%resname%~ #32 END ELSE BEGIN WRITE_ASCII DEATHVAR ~%deathvar%~ #32 END END
							PATCH_IF ~%sO%~ STRING_EQUAL ~+~ BEGIN WRITE_ASCII SCRIPT_OVERRIDE ~~ #8 END ELSE BEGIN PATCH_IF IS_AN_INT sO BEGIN WRITE_ASCII SCRIPT_OVERRIDE ~%resname%~ #8 END ELSE BEGIN WRITE_ASCII SCRIPT_OVERRIDE ~%sO%~ #8 END END
							PATCH_IF ~%sC%~ STRING_EQUAL ~+~ BEGIN WRITE_ASCII SCRIPT_CLASS ~~ #8 END ELSE BEGIN PATCH_IF IS_AN_INT sC BEGIN WRITE_ASCII SCRIPT_CLASS ~%resname%~ #8 END ELSE BEGIN WRITE_ASCII SCRIPT_CLASS ~%sC%~ #8 END END
							PATCH_IF ~%sR%~ STRING_EQUAL ~+~ BEGIN WRITE_ASCII SCRIPT_RACE ~~ #8 END ELSE BEGIN PATCH_IF IS_AN_INT sR BEGIN WRITE_ASCII SCRIPT_RACE ~%resname%~ #8 END ELSE BEGIN WRITE_ASCII SCRIPT_RACE ~%sR%~ #8 END END
							PATCH_IF ~%sG%~ STRING_EQUAL ~+~ BEGIN WRITE_ASCII SCRIPT_GENERAL ~~ #8 END ELSE BEGIN PATCH_IF IS_AN_INT sG BEGIN WRITE_ASCII SCRIPT_GENERAL ~%resname%~ #8 END ELSE BEGIN WRITE_ASCII SCRIPT_GENERAL ~%sG%~ #8 END END
							PATCH_IF ~%sD%~ STRING_EQUAL ~+~ BEGIN WRITE_ASCII SCRIPT_DEFAULT ~~ #8 END ELSE BEGIN PATCH_IF IS_AN_INT sD BEGIN WRITE_ASCII SCRIPT_DEFAULT ~%resname%~ #8 END ELSE BEGIN WRITE_ASCII SCRIPT_DEFAULT ~%sD%~ #8 END END
							PATCH_IF ~%dg%~ STRING_EQUAL ~+~ BEGIN WRITE_ASCII DIALOG ~~ #8 END ELSE BEGIN PATCH_IF IS_AN_INT dg BEGIN WRITE_ASCII DIALOG ~%resname%~ #8 END ELSE BEGIN WRITE_ASCII DIALOG ~%dg%~ #8 END END
							PATCH_IF NOT ~%portraitS%~ STRING_EQUAL ~+~ BEGIN WRITE_ASCII 0x34 ~%portraitS%~ #8 END
							PATCH_IF IS_AN_INT xpValue BEGIN
								PATCH_IF %xpValue% < 1000 BEGIN
									READ_LONG 0x14 THIS
									WRITE_LONG 0x14 (THIS * %xpValue%/100)
								END
								ELSE BEGIN
									WRITE_LONG 0x14 %xpValue%
								END
							END								
					END
				END		
			BUT_ONLY
		END
	END
END


//=============================================================================
DEFINE_ACTION_FUNCTION CopyToTemp STR_VAR res_type = ~~ res_path = ~~
BEGIN
	LAF Folder_Exists STR_VAR path = ~%res_path%~ folderName = ~%res_type%~ RET exists = result END
	ACTION_IF %exists% THEN BEGIN
		COPY ~%MOD_FOLDER%/%COMP_FOLDER%/%res_type%~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%~
	END	
END


//=============================================================================
DEFINE_ACTION_FUNCTION Compile STR_VAR res_type = ~~ res_path = ~~
BEGIN
	LAF Folder_Exists STR_VAR path = ~%res_path%~ folderName = ~%res_type%~ RET exists = result END
	ACTION_IF %exists% THEN BEGIN
		COMPILE ~%res_path%/%res_type%~
	END	
END


//=============================================================================
DEFINE_ACTION_MACRO ImportTranslationsBeforeAnySAYandCOMPILE_FromTDD
BEGIN
	ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/%COMP_FOLDER%/TRA.2da~ BEGIN
		COPY ~%MOD_FOLDER%/%COMP_FOLDER%/TRA.2da~ ~%MOD_FOLDER%/TEMP/%res_type%.2da~
			COUNT_2DA_ROWS 1 count
			WHILE (count > 1) BEGIN
				READ_2DA_ENTRY (count -1) 0 1 resname				
				INNER_ACTION BEGIN COPY ~TDD/Language/%LANGUAGE%/%resname%.tra~ ~%MOD_FOLDER%/TEMP/Translations/%resname%.tra~ END
				SET count = count -1
			END		
		BUT_ONLY
	END
END


//=============================================================================
DEFINE_ACTION_FUNCTION ImportResources_FromTDD STR_VAR res_type = ~~ res_dest = ~~
BEGIN		
	ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/%COMP_FOLDER%/%res_type%.2da~ BEGIN
		COPY ~%MOD_FOLDER%/%COMP_FOLDER%/%res_type%.2da~ ~%MOD_FOLDER%/TEMP/%res_type%.2da~
			COUNT_2DA_ROWS 1 count
			WHILE (count > 1) BEGIN
				READ_2DA_ENTRY (count -1) 0 1 resname				
				INNER_ACTION BEGIN COPY ~TDD/%res_type%/%resname%.%res_type%~ ~%res_dest%/%resname%.%res_type%~ END
				SET count = count -1
			END		
		BUT_ONLY
	END
END

//=============================================================================
DEFINE_ACTION_FUNCTION CopyResources STR_VAR res_type = ~~ res_dest = ~~
BEGIN
	LAF Folder_Exists STR_VAR path = ~%MOD_FOLDER%/%COMP_FOLDER%~ folderName = ~%res_type%~ RET exists = result END
	ACTION_IF %exists% THEN BEGIN
		COPY ~%MOD_FOLDER%/%COMP_FOLDER%/%res_type%~ ~override~
	END
END


//=============================================================================
//DEFINE_ACTION_FUNCTION Secure_Folder_Exists STR_VAR path = ~~ folderName = ~~ RET result
//BEGIN
//	OUTER_SET result = 0		
//	GET_DIRECTORY_ARRAY folders ~%path%~ ~%folderName%~
//	ACTION_PHP_EACH folders AS i => f BEGIN 
//		OUTER_SET result = 1
//		//PRINT ~%i% %f%~
//	END		
//END



//=============================================================================
DEFINE_ACTION_FUNCTION Folder_Exists STR_VAR path = ~~ folderName = ~~ RET result
BEGIN		
	OUTER_SET result = 0
	ACTION_PHP_EACH component_existing_folders AS i => f BEGIN
		ACTION_IF ~%f%~ STRING_EQUAL ~%path%/%folderName%~ 
			BEGIN OUTER_SET result = 1 END 		
	END
		
	//ACTION_IF result == 0 BEGIN
	//	PRINT ~Failed for <%path%> <%folderName%>~
	//	ACTION_PHP_EACH existing_folders AS i => f BEGIN		
	//		PRINT ~<%f%> <%path%/%folderName%>~
	//	END
	//END
END


//=============================================================================
DEFINE_ACTION_MACRO HandleAudio
BEGIN
	LAF HANDLE_AUDIO END	
	AT_NOW ~del /f /q .\TDDz\Audio\*.ogg 2> .\TDDz\Audio\error.txt~ EXACT // we redirect ugly error message when there is not file to delete		
END

//=============================================================================
DEFINE_ACTION_MACRO HandleTileset
BEGIN	
	//LAF HANDLE_TILESETS END
	LAF HANDLE_TILECONV
				STR_VAR
					input_path    = EVAL "%MOD_FOLDER%/tiz"
					output_path	  = "override"
	END
	AT_NOW ~del /f /q .\TDDz\Tiz\*.tiz 2> .\TDDz\Tiz\error.txt~ EXACT // we redirect ugly error message when there is not file to delete		
END
