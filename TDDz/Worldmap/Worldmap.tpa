//=============================================================================
DEFINE_ACTION_MACRO NotifyWorldmap
BEGIN
	LOCAL_SPRINT tutu_OrEmpty ~~
	ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
		OUTER_SPRINT tutuOrEmpty ~tutu_~
	END

	ACTION_IF FILE_EXISTS ~Worldmap/map_mods_areas.tbl~ && NOT FILE_CONTAINS ~Worldmap/map_mods_areas.tbl~ ~DD1900      DD1900~ THEN BEGIN		
		COPY ~Worldmap/map_mods_areas.tbl~ ~Worldmap~
			APPEND_FILE ~%MOD_FOLDER%/Worldmap/%tutu_OrEmpty%areas.tbl~		
			
		COPY ~Worldmap/map_mods_links.tbl~ ~Worldmap~
			APPEND_FILE ~%MOD_FOLDER%/Worldmap/%tutu_OrEmpty%links.tbl~
			PATCH_IF (FILE_EXISTS ~data/SoS-RULE.BIF~) BEGIN
				APPEND_FILE ~TDD/Worldmap/sos_links.tbl~
			END
			PATCH_IF (FILE_EXISTS ~data/ROT-RULE.BIF~) BEGIN
				APPEND_FILE ~TDD/Worldmap/rot_links.tbl~
			END
			PATCH_IF (FILE_EXISTS ~data/CTB-RULE.BIF~) BEGIN
				APPEND_FILE ~TDD/Worldmap/ctb_links.tbl~
			END
			
		COPY ~Worldmap/map_mods_trans.tra~ ~Worldmap~
			APPEND_FILE_EVALUATE ~%MOD_FOLDER%/Worldmap/%LANGUAGE%/worldmap.tra~
	END
	ELSE BEGIN
		MKDIR ~Worldmap~
		COPY ~%MOD_FOLDER%/Worldmap/areas.tbl~ ~Worldmap/map_mods_areas.tbl~
		COPY ~%MOD_FOLDER%/Worldmap/links.tbl~ ~Worldmap/map_mods_links.tbl~
		COPY ~%MOD_FOLDER%/Worldmap/%LANGUAGE%/worldmap.tra~ ~Worldmap/map_mods_trans.tra~
	END
	
	//EET Worldmap
	ACTION_IF (GAME_IS ~eet~) BEGIN
		COPY - ~TDDz/Worldmap/eet_areas.tbl~ ~.../TDDz/Worldmap~
			REPLACE_EVALUATE CASE_INSENSITIVE ~^\([A-Z0-9#_]+\)[ %TAB%]+\([A-Z0-9#_]+\)[ %TAB%]+\([A-Z0-9#_]+\)[ %TAB%]+\([0-9]+\)[ %TAB%]+\([0-9]+\)[ %TAB%]+\([0-9\-]+\)[ %TAB%]+\([0-9\-]+\)[ %TAB%]+\([A-Z0-9#_@]+\)[ %TAB%]+\([A-Z0-9#_@]+\)~ BEGIN
				SPRINT are_areName ~%MATCH1%~
				PATCH_FOR_EACH var IN MATCH8 MATCH9 BEGIN
					SPRINT match EVAL ~%%var%%~
					PATCH_IF ~%match%~ STRING_EQUAL_CASE ~N~ BEGIN
						SPRINT EVAL ~%var%~ ~~
					END ELSE BEGIN
						INNER_ACTION BEGIN
							COPY - ~TDDz/Worldmap/%LANGUAGE%/worldmap.tra~ ~.../TDDz/Worldmap/%LANGUAGE%~
								REPLACE_EVALUATE CASE_INSENSITIVE ~~~~~^\([ %TAB%]*%match%[ %TAB%]*=[ %TAB%]*[~"]\)\([^~^"]+\)\([~"]\)~~~~~ BEGIN
									INNER_PATCH_SAVE string "%MATCH2%" BEGIN
										REPLACE_TEXTUALLY "###" " "
									END
									SPRINT EVAL ~%var%~ ~%string%~
								END ~~
						END
					END
				END
				SPRINT are_strName ~%MATCH8%~
				SPRINT are_strDesc ~%MATCH9%~
				SET are_visible = 0
				SET are_visibleAdjacent = 0
				SET are_reachable = 0
				SET are_visited = 0
				PATCH_IF (MATCH4 BAND BIT0) BEGIN
					SET are_visible = 1
				END
				PATCH_IF (MATCH4 BAND BIT1) BEGIN
					SET are_visibleAdjacent = 1
				END
				PATCH_IF (MATCH4 BAND BIT2) BEGIN
					SET are_reachable = 1
				END
				PATCH_IF (MATCH4 BAND BIT3) BEGIN
					SET are_visited = 1
				END
				INNER_ACTION BEGIN
					LAF sc#addWmpAre
						INT_VAR
						mapIcon = MATCH5
						xCoord  = MATCH6
						yCoord  = MATCH7
						visible = are_visible
						visibleAdjacent = are_visibleAdjacent
						reachable = are_reachable
						visited = are_visited
						STR_VAR
						areName = EVAL "%are_areName%"
						strName = EVAL "%are_strName%"
						strDesc = EVAL "%are_strDesc%"
					END
				END
			END ~~
		COPY - ~TDDz/Worldmap/links.tbl~ ~.../TDDz/Worldmap/~
			REPLACE_EVALUATE CASE_INSENSITIVE ~^\([A-Z0-9#_]+\)[ %TAB%]+\([A-Z]+\)[ %TAB%]+\([A-Z0-9#_]+\)[ %TAB%]+\([A-Z0-9#_]+\)[ %TAB%]+\([0-9]+\)[ %TAB%]+\([0-9]+\)[ %TAB%]+\([A-Z0-9#_]+\)[ %TAB%]+\([A-Z0-9#_]+\)[ %TAB%]+\([A-Z0-9#_]+\)[ %TAB%]+\([A-Z0-9#_]+\)[ %TAB%]+\([A-Z0-9#_]+\)[ %TAB%]+\([0-9]+\)~ BEGIN
				PATCH_FOR_EACH var IN MATCH4 MATCH7 MATCH8 MATCH9 MATCH10 MATCH11 BEGIN
					SPRINT match EVAL ~%%var%%~
					PATCH_IF ~%match%~ STRING_EQUAL_CASE ~N~ BEGIN
						SPRINT EVAL ~%var%~ ~~
					END
				END
				INNER_ACTION BEGIN
					COPY_EXISTING ~worldmap.wmp~ ~override~
						LPF ADD_WORLDMAP_LINKS
							INT_VAR
							distance_scale = MATCH5
							default_entry = MATCH6
							encounter_probability = MATCH12
							STR_VAR
							from_area = EVAL ~%MATCH1%~
							from_node = EVAL ~%MATCH2%~
							to_area = EVAL ~%MATCH3%~
							entry = EVAL ~%MATCH4%~
							random_area1 = EVAL ~%MATCH7%~
							random_area2 = EVAL ~%MATCH8%~
							random_area3 = EVAL ~%MATCH9%~
							random_area4 = EVAL ~%MATCH10%~
							random_area5 = EVAL ~%MATCH11%~
						END
					BUT_ONLY
				END
			END ~~
	END	
END


