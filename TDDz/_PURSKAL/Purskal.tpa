//=============================================================================
DEFINE_ACTION_MACRO FinalizeImport
BEGIN
	INCLUDE ~%MOD_FOLDER%/Lib/Areas.tpa~

	APPEND ~GTIMES.IDS~ ~12 TWO_ROUNDS~ 		UNLESS ~TWO_ROUNDS~
	APPEND ~GTIMES.IDS~ ~180 THREE_TURNS~      	UNLESS ~THREE_TURNS~
	
	APPEND ~SPELL.IDS~ 	~1963 FRIGHTFUL_MOAN~	UNLESS ~1963~	
		
	APPEND ~MASTAREA.2DA~ ~DD1900   value~ UNLESS ~DD1900   value~
	
	COPY ~%MOD_FOLDER%/%COMP_FOLDER%/BAM/DDPOTNP2.BAM~ ~override~	
	COPY_EXISTING ~CITTERNZ.itm~ ~override~ SAY NAME1 #38789
		
	LAM HandleBathedInBlood	
	LAM HandleSpellScrollsContract
	LAM HandleItemsContract		
	
	EXTEND_TOP ~AR0300.bcs~ ~%MOD_FOLDER%/%COMP_FOLDER%/Snip/bAR0300.baf~
	EXTEND_TOP ~AR0902.bcs~ ~%MOD_FOLDER%/%COMP_FOLDER%/Snip/nAR0902.baf~
	
	ACTION_IF FILE_EXISTS_IN_GAME ~AR0509.bcs~ BEGIN
		EXTEND_TOP ~AR0509.bcs~ ~%MOD_FOLDER%/%COMP_FOLDER%/Snip/xAR0509.baf~ // From: AR0509.BAF
	END
	ELSE BEGIN
		COPY ~%MOD_FOLDER%/%COMP_FOLDER%/Snip/xAR0509.baf~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/AR0509.baf~		
		COPY_EXISTING ~AR0509.ARE~ ~override~ 
			WRITE_ASCII 0x94 ~AR0509~ #8
			LPM SetAnimationsInCombatFlag
	END
	
	OUTER_TEXT_SPRINT sender ~%COMP_FOLDER%~
	INCLUDE ~%MOD_FOLDER%/Lib/Helpers.tpa~ LAF Accept STR_VAR sender target = ~_PURSKAL~ END
END

//=============================================================================
DEFINE_ACTION_MACRO HandleItemsContract
BEGIN
	COPY ~%MOD_FOLDER%/%COMP_FOLDER%/ItemsContract.2da~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/ItemsContract.2da~
		COUNT_2DA_ROWS 7 count
		WHILE (count > 1) BEGIN
			READ_2DA_ENTRY (count -1) 0 7 resname				
			READ_2DA_ENTRY (count -1) 6 7 undroppable
			SET count = count -1
						
			PATCH_IF NOT ~%undroppable%~ STRING_EQUAL ~+~ BEGIN			
				TO_UPPER resname				
				INNER_ACTION BEGIN					
					ACTION_IF ~%undroppable%~ STRING_EQUAL ~.~ 
						BEGIN OUTER_SPRINT value ~SCRL75~ END 
						ELSE BEGIN OUTER_SPRINT value ~%undroppable%~ END
					
					ACTION_MATCH ~%resname%~ 
					WITH
						~CRYPTXX~ 	BEGIN COPY_EXISTING ~DD3501.ARE~ ~override~ WRITE_ASCII 0xA34 ~%value%~ #8 END
						~MISERY~ 	BEGIN COPY_EXISTING ~DD3501.ARE~ ~override~ WRITE_ASCII 0xB4C ~%value%~ #8 END						
						~TYRANT~ 	BEGIN COPY_EXISTING ~DD3501.ARE~ ~override~ WRITE_ASCII 0xC50 ~%value%~ #8 END						
						DEFAULT
					END
				END
			END
		END
	BUT_ONLY
END

//=============================================================================
DEFINE_ACTION_MACRO HandleBathedInBlood
BEGIN
	COPY ~%MOD_FOLDER%/_CORE/ItemsContract.2da~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/ItemsContract.2da~
		COUNT_2DA_ROWS 7 count
		WHILE (count > 1) BEGIN
			READ_2DA_ENTRY (count -1) 0 7 resname				
			READ_2DA_ENTRY (count -1) 6 7 undroppable
			SET count = count -1
				
			TO_UPPER resname
			PATCH_IF ~%resname%~ STRING_EQUAL ~BLOOD~ && NOT ~%undroppable%~ STRING_EQUAL ~+~ && NOT ~%undroppable%~ STRING_EQUAL ~.~ BEGIN				
				INNER_ACTION BEGIN
					ACTION_FOR_EACH cre IN ~THORK1~ ~THORK2~ ~THORK3~ ~THORK4~ ~WTTHORK5~ ~WTTHORK6~ 
						BEGIN COPY_EXISTING ~%cre%.cre~ ~override~ WRITE_ASCII 0x3E4 ~%undroppable%~ #8 END
				END
			END
		END
	BUT_ONLY
END


//=============================================================================
DEFINE_ACTION_MACRO HandleSpellScrollsContract
BEGIN
	COPY ~%MOD_FOLDER%/_CORE/SpellScrollsContract.2da~ ~%MOD_FOLDER%/TEMP/%COMP_FOLDER%/SpellScrollsContract.2da~
		COUNT_2DA_ROWS 8 count
		WHILE (count > 1) BEGIN
			READ_2DA_ENTRY (count -1) 0 8 resname				
			READ_2DA_ENTRY (count -1) 6 8 undroppable
			SET count = count -1
						
			PATCH_IF NOT ~%undroppable%~ STRING_EQUAL ~+~ BEGIN			
				TO_UPPER resname				
				INNER_ACTION BEGIN					
					ACTION_IF ~%undroppable%~ STRING_EQUAL ~.~ // '.' means undroppable
						BEGIN OUTER_SPRINT value ~SCRL75~ END 
						ELSE BEGIN OUTER_SPRINT value ~%undroppable%~ END
					
					ACTION_MATCH ~%resname%~ 
					WITH
						~SCRX3~ 	BEGIN COPY_EXISTING ~DD3501.ARE~ ~override~ WRITE_ASCII 0xA98 ~%value%~ #8 END
						~SCRX7~ 	BEGIN COPY_EXISTING ~DD3503.ARE~ ~override~ WRITE_ASCII 0xAD4 ~%value%~ #8 END
						~SCRX8~ 	BEGIN COPY_EXISTING ~DD3501.ARE~ ~override~ WRITE_ASCII 0xB24 ~%value%~ #8 END
						~SCRX13~ 	BEGIN COPY_EXISTING ~DD3501.ARE~ ~override~ WRITE_ASCII 0xC14 ~%value%~ #8 END
						~SCRX22~ 	BEGIN COPY_EXISTING ~DD3501.ARE~ ~override~ WRITE_ASCII 0xA5C ~%value%~ #8 END
						~SCRX32~ 	BEGIN COPY_EXISTING ~DD3503.ARE~ ~override~ WRITE_ASCII 0xC00 ~%value%~ #8 END
						DEFAULT
					END
				END
			END
		END
	BUT_ONLY	
END


